apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    defaultConfig {
        applicationId "eu.quelltext.mundraub"
        minSdkVersion 9
        targetSdkVersion 25

        // BEGIN: this is changed by the travis build
        versionCode 216
        versionName "v1.215"
        // END

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        // see https://chris.banes.me/2016/02/25/appcompat-vector/#enabling-the-flag
        vectorDrawables.useSupportLibrary = true
    }
    // sign the release on Travis, see https://doubledot.dev/blog/2018-11-14-Continuous-Integration/
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def isRunningOnTravis = System.getenv("SIGN_THE_BUILD") == "true"
    if (isRunningOnTravis) {
        signingConfigs {
            release
        }
        signingConfigs.release.storeFile = file("../.travis/key.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
        buildTypes.release.signingConfig signingConfigs.release
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:25.4.0'
    implementation 'com.android.support:design:25.4.0'
    testImplementation 'junit:junit:4.12'
    // this is the last support test for api >= 9
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'
    implementation 'com.android.support:recyclerview-v7:25.4.0'
    implementation 'commons-io:commons-io:2.4'
    // from https://stackoverflow.com/a/23810852/1320237
    // from https://stackoverflow.com/a/31372941/1320237
    implementation files('libs/commons-lang3-3.7.jar')
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'//files('libs/okhttp-3.11.0.jar')

    testImplementation 'org.json:json:20180130' // from https://stackoverflow.com/a/41398830/1320237
    testImplementation "org.robolectric:robolectric:4.0.2"
    testImplementation 'androidx.test:core:1.0.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'

    // add espresso for testing
    // see https://www.vogella.com/tutorials/AndroidTestingEspresso/article.html
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
}
